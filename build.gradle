apply plugin: 'java'
apply plugin: 'eclipse'

sourceCompatibility = 1.8
archivesBaseName = 'Stargate'
version = getCheckedOutGitCommitHash()

repositories {
   flatDir {
       dirs 'libs'
   }
}

dependencies {
   compile name: 'Minecraft_Mod'
}

def getCheckedOutGitCommitHash() {
    def gitFolder = "$projectDir/.git/"
    def takeFromHash = 12
    /*
     * '.git/HEAD' contains either
     *      in case of detached head: the currently checked out commit hash
     *      otherwise: a reference to a file containing the current commit hash
     */
    def head = new File(gitFolder + "HEAD").text.split(":") // .git/HEAD
    def isCommit = head.length == 1 // e5a7c79edabbf7dd39888442df081b1c9d8e88fd
    // def isRef = head.length > 1     // ref: refs/heads/master

    if(isCommit) return head[0].trim().take(takeFromHash) // e5a7c79edabb

    def refHead = new File(gitFolder + head[1].trim()) // .git/refs/heads/master
    refHead.text.trim().take takeFromHash
}

jar {
	manifest {
        attributes 'Implementation-Title': archivesBaseName,  
        	'Implementation-Version': version,
        	'Main-Class': 'Stargate'
    }
}